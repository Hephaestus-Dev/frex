// Much of this stolen with permission from https://github.com/CottonMC/cotton/blob/master/build.gradle

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

buildscript {
	repositories {
		mavenCentral()
		jcenter()
		maven {
			name = "Fabric"
			url = "http://maven.modmuss50.me/"
		}
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath "net.fabricmc:fabric-loom:0.2.0-SNAPSHOT" //Don't backdate before 0.2.0; features in this version prevent messy workarounds!
		classpath "com.github.jengelman.gradle.plugins:shadow:4.0.2"
		classpath 'org.ajoberstar:gradle-git:0.10.1'
	}
}

// see https://github.com/passsy/gradle-GitVersioner
ext.gitVersioner = [
        defaultBranch           : "master",  // default "master"
        stableBranches          : ["master"], // default [], the feature branch postfix (-dm4(6)) will not be appended on stable branches, all commits are included into the version number calculation
        yearFactor              : 1200, 	  // default "1000", increasing every 8.57h
        snapshotEnabled         : false,      // default false, the "-SNAPSHOT" postfix
        localChangesCountEnabled: false,       // default false, the (<commitCount>) before -SNAPSHOT
        shortName: { gitVersion ->            // optional closure to build a short name
          // allows you to add your own short name logic
          // All properties from gitVersion are available
          // can be used for CI `System.getenv("BUILD_NUMBER")`

          // i.e. use short sha1
          return gitVersion.commit.subSequence(0, 7)
        }
]

// import the script which runs the version generation
apply from: 'https://raw.githubusercontent.com/passsy/gradle-GitVersioner/master/git-versioner.gradle'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: "maven-publish"
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: net.fabricmc.loom.LoomGradlePlugin

repositories {
    //flatDir {
    //    dirs '../fabric/build/libs'
    //}
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

archivesBaseName = "frex"
group = "grondag"
version = "${mod_version}.${gitVersionName}-${release_type}"

minecraft {
}

sourceSets {
	main {
	        java {
	            include 'grondag/**/*'
	            exclude '*.DS_Store'
	        }
	        resources
	        {
	        	include '**/*'
	        	exclude '*.DS_Store'
	        }
	    }
    test {
        java {
            exclude 'grondag/**'
        }
    }
}

dependencies {
	minecraft "com.mojang:minecraft:1.14 Pre-Release 1"
	mappings "net.fabricmc:yarn:1.14 Pre-Release 1+build.3"
	modCompile "net.fabricmc:fabric-loader:0.4.0+build.119"
	modCompile "net.fabricmc:fabric:0.2.7+build.122"
	
	compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.0'
	
	shadow "org.joml:joml:1.9.9"
	implementation "org.joml:joml:1.9.9"
}

task sourcesJar(type: Jar) {
	baseName = archivesBaseName
	classifier = "sources"
	from sourceSets.main.allSource
	from sourceSets.main.resources
}

tasks.jar.configure {
  classifier = 'default'
}

shadowJar {
	classifier = null
	configurations = [project.configurations.shadow]
}

remapJar {
	dependsOn shadowJar
	jar = shadowJar.archivePath
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            version project.version
            artifactId project.archivesBaseName
            groupId project.group
            
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }
            
            artifact shadowJar {
                classifier null
            }
        }
    }

    repositories {
        maven {
            url "https://grondag-repo.appspot.com"
            credentials {
	            username "admin"
	            password "${maven_password}"
			}
        }
    }
}